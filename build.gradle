plugins {
	id 'fabric-loom' version '0.12.+'
	id 'maven-publish'
}

JavaLanguageVersion targetVersion = JavaLanguageVersion.of(17)

allprojects {
    // Allow more then 100 errors to be displayed.
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "${project.max_errors}"
        }
    }
}


archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = "Fabric"
		url = "https://maven.fabricmc.net/"
	}
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		url = "https://maven.shedaniel.me/"
	}
	maven { url 'https://maven.wispforest.io/' } // owo lib
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cardinal_version}"))

	modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cardinal_version}"))

	// Implement Trinkets for parachutes, rings etc.
	modImplementation(include("dev.emi:trinkets:${project.trinkets_version}"))

	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"

	modImplementation(include("io.github.ladysnake:satin:${project.satin_version}"))
	modImplementation "io.wispforest:owo-lib:${project.owo_version}+1.18"
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
}

loom {
	accessWidenerPath = file("src/main/resources/incubus_core.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

java {
    toolchain {
        languageVersion = targetVersion
    }
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId archivesBaseName
			from components.java
		}
	}
	repositories {
		maven {
			url ='s3://maven.kyrptonaught.dev'
			credentials(AwsCredentials) {
				accessKey = System.getenv('maven-key-id')
				secretKey = System.getenv('maven-key')
			}
		}
	}
}